buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.1.8.RELEASE'
        classpath 'com.ofg:uptodate-gradle-plugin:+'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:+'
        classpath 'pl.allegro.tech.build:axion-release-plugin:0.9.3'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.4'
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.5'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

apply plugin: 'axion-release'
apply plugin: 'cobertura'
apply plugin: 'versions'

scmVersion {
    tag {
        prefix = ''
        versionSeparator = ''
    }
}

allprojects {
    group = 'com.ofg'
    project.version = scmVersion.version
}

ext {
    microDepsSpringConfigVersion = '0.5.0'
    groovyVersion = '2.3.7'
    spockVersion = '0.7-groovy-2.0'
    springBootVersion = '1.1.8.RELEASE'
    springTestVersion = '4.1.1.RELEASE'
}

task addHashFile << {
    String gitCmd = "git log --pretty=format:'%H' -n 1"
    def proc = gitCmd.execute()
    proc.waitFor()
    new File("commitHash.txt").withWriter { it << proc.in.text }
}

def srcSubprojects = subprojects - project(':swagger')
configure(srcSubprojects) {
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.ofg.uptodate'

    sourceCompatibility = 1.7

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    //Workaround for the issue with Java 8u11 and 7u65 - http://www.infoq.com/news/2014/08/Java8-U11-Broke-Tools
    test {
        jvmArgs '-noverify'
    }

    bintrayUpload.dependsOn 'build'

    bintray {
        user = System.properties['bintrayUser']
        key = System.properties['bintrayKey']

        publications = ['mavenJava']
        pkg {
            repo = 'micro'
            userOrg = '4finance'
            name = project.name
            desc = project.description
            licenses = ['Apache-2.0']
            labels = ['microservice']
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    jar {
        dependsOn addHashFile
        from "commitHash.txt"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: groovydoc) {
        classifier = 'javadoc'
        from groovydoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    if(project.hasProperty("coverage")) {
        apply plugin: 'cobertura'


        cobertura {
            coverageIgnoreTrivial = true
            coverageFormats = [ 'xml', 'html' ]
        }

        test {
            ignoreFailures = true
        }
    }
}

def files = srcSubprojects.collect { new File(it.projectDir, '/build/cobertura/cobertura.ser') }
cobertura {
    coverageFormats = [ 'xml', 'html' ]
    coverageMergeDatafiles = files
}
test.dependsOn(srcSubprojects.test)